# NoLockScreen for Windows8
# Version 2.2 (2014/04/01) -- ソフトウェアタイトルの修正
# Version 2.2 (2014/02/08) -- URL修正/リンクの追加/なでしこエンジンの更新
# Version 2.1 (2014/01/05) -- Win8.1に正式対応
# Version 2.0 (2013/10/09)
# Version 1.0 (2013/09/15)
# Copyright (C) 2013-2014 nakkaa.

# ソースコードだと正常に動きません。
# 実行する際はexeファイルに変換してください。(管理者権限実行がうまく動作しないため)

!VER=「2.2」

//エラーダイアログの表示を不許可に
エラーダイアログ表示許可=0

母艦を最前面

//Windows8でなければ終了
もし、(OSバージョン=6.2)OR(OSバージョン=6.3)でなければ、
　「このソフトウェアはWindows8か、8.1でのみ動作します。」という
　終了

もし、コマンドライン\1が『-debug::*』にワイルドカード一致ならば、
　コマンドライン\1は空

もし、コマンドライン\1が「root」でなければ、
　再起動処理

//メインウィンドウのプロパティ
!母艦設計=「
母艦について
　可視はオフ
　タイトルは『NoLockScreen for Windows8』
　スタイルは『ダイアログスタイル』
　H=250
　W=400
母艦を中央移動」

//書き込む対象のレジストリパス　
!変更対象=「HKEY_LOCAL_MACHINE\SOFTWARE\Policies\Microsoft\Windows\Personalization」

//現在の状態を確認
もし、(変更対象にレジストリキー存在)が「1」ならば、
　S=変更対象のレジストリ開く
　Sで「NoLockScreen」をレジストリ整数読んでNLSへ代入
　Sのレジストリ閉じる
違えば、NLS=0


//現在の状態を表示
情報とはラベル
これについて
　X=80
　Y=30
　W=300
　文字書体は「Meiryo UI|12」

//実行ボタン
実行とはボタン
これについて
　X=80
　Y=80
　H=80
　W=240
　文字書体は「Meiryo UI|12」
　クリックした時は〜
　　実行のテキスト&「しました」をいう
　　もし、NLS=0ならば、LS無効化処理
　　違えば、LS有効化処理
　　終了

もし、NLS=0ならば、LS有効時文字列
違えば、LS無効時文字列

//バージョン情報
改訂とはラベル
これについて
　X=300
　Y=195
　文字書体は「Meiryo UI|10」
　テキストは「Version 」&VER
　　
URLとはラベル
これについて
　X=5
　Y=195
　文字書体は「Meiryo UI|10|下線」
　文字色は$66cc
　マウス移動した時は〜
　　(0の32649のカーソルアイコン取得)にマウスカーソル設定。
　マウス押した時は〜
　　(0の32649のカーソルアイコン取得)にマウスカーソル設定。
　テキストは「(C) 2013 nakkaa 」
　クリックした時は〜
　　「http://7ka.org/」を起動

//ロックスクリーン有効時のラベル文字列
●LS有効時文字列
　情報のテキスト=「このPCのロック画面は 有効 です。」
　実行のテキスト=「ロック画面を無効化」

//ロックスクリーン無効時のラベル文字列
●LS無効時文字列
　情報のテキスト=「このPCのロック画面は 無効 です。」
　実行のテキスト=「ロック画面を有効化」
　
//ロックスクリーン有効化
●LS有効化処理
　エラー監視
　　S=変更対象のレジストリ開く
　　Sで「NoLockScreen」に「0」をレジストリ整数書く
　　Sのレジストリ閉じる
　　NLS=0
　エラーならば、
　　エラー処理
//ロックスクリーン無効化
●LS無効化処理
　エラー監視
　　S=変更対象のレジストリ開く
　　Sで「NoLockScreen」に「1」をレジストリ整数書く
　　Sのレジストリ閉じる
　　NLS=1
　エラーならば、
　　エラー処理

●再起動処理
　エラー監視
　　ナデシコランタイムパス&「 "root" 」を管理者権限実行。
　エラーならば、エラー処理
　終了
●エラー処理
　「■エラーが発生しました■{~}{~}{エラーメッセージ}{~}アプリケーションを終了します。」という
　終了
●カーソルアイコン取得(ファイルの種類の) = DLL("user32.dll","DWORD LoadCursorA(BOOL,DWORD)")
●マウスカーソル設定(Iに) = DLL("user32.dll","DWORD SetCursor(DWORD)")

母艦の可視はオン


